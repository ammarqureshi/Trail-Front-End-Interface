PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX osi: <http://ontologies.geohive.ie/osi#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX sf: <http://www.opengis.net/ont/sf#>

PREFIX trail: <http://example.org/trail/>

# Find the number of trails in current district.
#   Parameters:  current long and lat
#   Returns:     districtName, number of trails

SELECT ?districtName (count(distinct ?trail) AS ?count)
WHERE {
  ?district a osi:Municipal_District.
  ?district rdfs:label ?districtName.
  ?district geo:hasGeometry ?geom.
  ?geom geo:asWKT ?wkt.

  ?trail a trail:Trail.
  ?trail trail:name ?trailName.
  ?trail trail:startPoint ?spGeom.
  ?spGeom geo:asWKT ?trailStartPoint.

  # Get the current district. POINT(LONG, LAT)
  FILTER(geof:sfWithin("POINT(%PARAM_0% %PARAM_1%)"^^sf:wktLiteral, ?wkt) && geof:sfWithin(?trailStartPoint, ?wkt)).
} GROUP BY ?districtName