PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX osi: <http://ontologies.geohive.ie/osi#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX sf: <http://www.opengis.net/ont/sf#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX trail: <http://example.org/trail/>

# Rank trails within a district based on Estimated Completion Time and that allow dogs.
#   Parameters:
#   Returns:     district, trailName, startPoint, completionTime, dogsAllowed

SELECT DISTINCT ?province ?districtName ?trailName ?trailStartPoint ?trailEstimatedCompletionTime ?trailDogsAllowed
WHERE {

  ?district a osi:Municipal_District.
  ?district rdfs:label ?districtName.
  ?district geo:hasGeometry ?geom.
  ?geom geo:asWKT ?wkt.

  ?trail a trail:Trail.
  ?trail trail:name ?trailName.
  ?trail trail:startPoint ?spGeom.
  ?spGeom geo:asWKT ?trailStartPoint.
  ?trail trail:dogs_allowed ?trailDogsAllowed.
  ?trail trail:estimated_time_to_complete ?trailEstimatedCompletionTime.
  ?trail trail:locatedIn ?county.
  ?county trail:locatedIn ?province.

  FILTER(geof:sfWithin(?trailStartPoint, ?wkt) && ?trailDogsAllowed = "true"^^xsd:boolean).
  
  # Remove duplicates that have the @en language tag
  FILTER ( LANG(?districtName) = "" && LANG(?trailName) = "").

}ORDER BY ASC(?trailEstimatedCompletionTime)